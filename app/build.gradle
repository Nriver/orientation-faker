apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "com.github.ben-manes.versions"
apply plugin: "com.google.firebase.crashlytics"
apply plugin: "com.google.firebase.firebase-perf"

def applicationName = "OrientationFaker"
def versionMajor = 4
def versionMinor = 2
def versionPatch = 5

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "net.mm2d.android.orientationfaker"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        resConfigs "en", "ja"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "long", "BUILD_TIME", "${System.currentTimeMillis()}L"
        archivesBaseName = "${applicationName}-${versionName}"
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix = ".debug"
            versionNameSuffix = "d"
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name != "release") {
            return
        }
        variant.outputs.all {
            outputFileName = "${applicationName}-${versionName}-google-play.apk"
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.core:core-ktx:1.3.0"
    implementation "androidx.browser:browser:1.2.0"
    implementation "androidx.legacy:legacy-support-v13:1.0.0"
    implementation "androidx.lifecycle:lifecycle-process:2.2.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation "androidx.preference:preference:1.1.1"
    implementation "com.google.android.material:material:1.1.0"
    implementation "com.google.android.play:core:1.7.3"
    implementation "com.google.android.play:core-ktx:1.7.0"
    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-ktx:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"
    implementation "net.mm2d:log:0.9.1"
    implementation "net.mm2d:log-android:0.9.1"
    implementation "net.mm2d:color-chooser:0.1.6"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.3"

    implementation "com.google.firebase:firebase-core:17.4.3"
    implementation "com.google.firebase:firebase-ads:19.1.0"
    implementation "com.google.firebase:firebase-perf:19.0.7"
    implementation "com.google.firebase:firebase-crashlytics:17.0.1"
    implementation "com.google.firebase:firebase-analytics:17.4.3"
    implementation "com.google.android.ads.consent:consent-library:1.0.8"
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview", "b", "ea"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
